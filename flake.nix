{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs?ref=nixos-unstable";
    flake-parts = {
      url = "github:hercules-ci/flake-parts";
      inputs.nixpkgs-lib.follows = "nixpkgs";
    };
    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs:
    inputs.flake-parts.lib.mkFlake { inherit inputs; } {
      systems = [
        "x86_64-linux"
        "aarch64-linux"
        "x86_64-darwin"
        "aarch64-darwin"
      ];
      perSystem =
        {
          system,
          config,
          pkgs,
          inputs',
          ...
        }:
        {
          _module.args.pkgs = import inputs.nixpkgs {
            inherit system;
            overlays = [
              inputs.rust-overlay.overlays.default
            ];
          };

          devShells.default =
            with pkgs;
            mkShell {
              nativeBuildInputs = [
                (rust-bin.stable.latest.default.override {
                  extensions = [ "rust-src" ];
                })
              ];

              buildInputs = [ alsa-lib ];

              # https://github.com/not-fl3/macroquad/issues/641#issuecomment-1760137883
              LD_LIBRARY_PATH = builtins.concatStringsSep ":" [
                "${pkgs.libx11}/lib"
                "${pkgs.libxi}/lib"
                "${pkgs.libGL}/lib"
                "${pkgs.libxkbcommon}/lib"
              ];
            };
        };
    };
}
